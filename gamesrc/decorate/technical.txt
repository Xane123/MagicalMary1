//All objects defined in this file are invisible objects technically used for
//gameplay elements, such as automatically-opening doors.

Actor AutoDoorOpener 457	//ARGS
							//0: Distance
							//1: Sector ID
							//2: Type (check ACS script for list)
{
Radius 16
Height 32
+ISMONSTER
-SOLID
+NOBLOCKMAP
+NOGRAVITY
+INVISIBLE
+INVULNERABLE
-COUNTITEM
-COUNTKILL
//$Title Automatic Door Opener
//$Category Technical
//$Arg0 Radius
//$Arg1 ID
//$Arg2 Type
//$Arg0RenderStyle Circle
States
{
Spawn:
TNT1 A 0
See:
TNT1 A 5
TNT1 A 0 A_JumpIf(Args[2]==5,"See_Obj")	//If this is an object check, use the multiplier CVAR!
TNT1 A 0 A_CheckProximity("OpenDoor","PlayerPawn",Args[0],1,CPXF_ANCESTOR)
Loop
See_Obj:
TNT1 A 0 A_CheckProximity("OpenDoor","PlayerPawn",CallACS("CalculateOBJDrawDistance",Args[0],0,0,0),1,CPXF_ANCESTOR)
Goto See
OpenDoor:
TNT1 A 0
TNT1 A 30 ACS_NamedExecuteAlways("AutoDoorOpener_Command",0,Args[1],Args[2],TRUE)
WaitX:
TNT1 A 5
TNT1 A 0 A_JumpIf(Args[2]==5,"WaitX_Obj")	//If this is an object check, use the multiplier CVAR!
TNT1 A 0 A_CheckProximity("WaitX","PlayerPawn",Args[0],1,CPXF_ANCESTOR)
CloseDoor:
TNT1 A 0
TNT1 A 30 ACS_NamedExecuteAlways("AutoDoorOpener_Command",0,Args[1],Args[2],FALSE)
Goto See
WaitX_Obj:
TNT1 A 0 A_CheckProximity("WaitX","PlayerPawn",CallACS("CalculateOBJDrawDistance",Args[0],0,0,0),1,CPXF_ANCESTOR)
Goto CloseDoor
}
}

Actor SpecialPlane 470
{
Radius 16
Height 32
+ISMONSTER	//Just in case making this object "dormant" is necessary.
-SOLID
+NOBLOCKMAP
+NOGRAVITY
+INVISIBLE
+INVULNERABLE
-COUNTITEM
-COUNTKILL
//$Title Special Plane Properties
//$Category Technical
//$Arg0 Radius
//$Arg1 ID
//$Arg2 Behavior
//$Arg0RenderStyle Rectangle
States
{
Spawn:
TNT1 A 0
TNT1 A 0 ACS_NamedExecuteAlways("SetPlaneProperties",0,Args[1],Args[2],Args[0])
//TNT1 A 0 A_JumpIf(Args[1]==0,"See")
Death:
TNT1 A 1 A_FadeOut(0.5,1)
Loop
//See:
//TNT1 A 105 ACS_NamedExecuteAlways("SetPlaneProperties",0,Args[1],Args[2],Args[0])
//Loop
}
}