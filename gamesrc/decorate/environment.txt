//299 reserved for coins.

Actor Spring 257
{
-SOLID
+NOGRAVITY
+NOINTERACTION
//$Category obstacles
//$Sprite SPRIC
States
{
Spawn:
SPRI A 0
SPRI A 0 A_JumpIf(args[0]==1,"Vertical")
SPRI A 0 A_JumpIf(args[0]==2,"Horizontal")
Diagonal:
SPRI A 0 A_JumpIf(z-floorz>7.5,"Diagonal_Support")
SPRI A -1
Stop
Diagonal_Support:
SPRI D -1
Stop
Vertical:
SPRI B -1
Stop
Horizontal:
SPRI C -1
Stop
}
}

Actor PalmTree 2016
{
Radius 5
Height 48
+SOLID
//$Title Palm Tree
//$Category Trees
States
{
Spawn:
TNT1 A 0 NoDelay A_Jump(128,"S","M","XL")
Goto L
S:
PALM A -1
Stop
M:
PALM B -1
Stop
L:	
PALM C -1
Stop
XL:
PALM D -1
Stop
}
}

Actor Icicle 2017
{
Radius 8
Height 2
+NOGRAVITY
States
{
Spawn:
TNT1 "#" 0
TNT1 "#" 0 A_Jump(128,"S","L")
M:
ICIC B 0
Goto Render
S:
ICIC A 0
Goto Render
L:
ICIC C 0
Render:
ICIC "#" 0 A_ChangeVelocity(Random(-0.1,0.1),Random(-0.1,0.1),10,CVF_REPLACE)
ICIC "#" 0 A_JumpIf(ceilingz-z<1.0,"RenderDone")
ICIC "#" 1
Loop
RenderDone:
ICIC"#" -1
Stop
}
}

Actor FlightWarpstar 2018
{
Radius 16
Height 16
+RANDOMIZE
States
{
Spawn:
FWSM A -1
Stop
}
}

Actor PlantA 2020
{
Radius 16
Height 16
Scale 0.34
+RANDOMIZE
//$Title Potted Plant
//$Category decoration/Hell City
States
{
Spawn:
PLAN A -1
Stop
}
}

Actor BushA 2021
{
Radius 16
Height 16
Scale 0.75
-SOLID
+FLOORHUGGER
+FLOORCLIP
+CANTLEAVEFLOORPIC
//$Title Bush
//$Arg0Title Randomize scale
States
{
Spawn:
BUSH A 0
BUSH A 0 A_JumpIf(Args[0]==0,"See")
BUSH A 3 A_ChangeVelocity(x%14+Random(-24,16),y%14+Random(-24,16),0,CVF_REPLACE|CVF_RELATIVE)
See:
BUSH A 0 A_SetScale(scalex+(((z+x-y)%3)/3)+Random(-0.3,0.2))
BUSH A -1 A_Stop
Stop
}
}

Actor BoostPanel 258
{
Radius 32
Height 64
Scale 0.5
-SOLID
//$Title Boost Panel
//$Category obstacles
States
{
Spawn:
BOST A -1
Loop
}
}

Actor AirRing 272
{
Radius 33
Height 48
Scale 3
+NOGRAVITY
-SOLID
//$Title Ring
//$Category obstacles
//$Sprite RINGA
States
{
Spawn:
TNT1 A 0
Pain:
TNT1 A 0 A_CheckProximity("See","PlayerPawn",512.0,1,CPXF_ANCESTOR)
RING B 3 BRIGHT
Loop
See:
RING A 3
Goto Pain
}
}

actor AirSpawner_Weakness : Inventory { Inventory.MaxAmount 8 }
actor MaryUmbrella_Air : Inventory { Inventory.MaxAmount 1 }

actor OpenGL_AirParticle1
{
Radius 4
Height 8
Scale 0.1
RenderStyle Add
-SOLID
+NOGRAVITY
+BRIGHT
+FORCEXYBILLBOARD
States
{
Spawn:
PART A 1 A_FadeOut(0.06,1)
Loop
}
}
actor OpenGL_AirParticle2 : OpenGL_AirParticle1 { Scale 0.2 }
actor OpenGL_AirParticle3 : OpenGL_AirParticle2 { Scale 0.34 }

actor AirCurrent1 259
{
Radius 32
Height 280
+NOGRAVITY
-SOLID
+ISMONSTER	//This is required for "DORMANT" to work.
-COUNTKILL
//$Title Air Current (size 1, ~280 high)
//$Sprite EDITAIC1
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_CheckProximity("SpawnParticles","PlayerPawn",384.0,1,CPXF_ANCESTOR)
TNT1 A 0 A_CheckProximity("LOD1","PlayerPawn",512.0,1,CPXF_ANCESTOR)
TNT1 A 0 A_CheckProximity("LOD2","PlayerPawn",2000.0,1,CPXF_ANCESTOR)
TNT1 A 0 A_CheckProximity("LOD3","PlayerPawn",3200.0,1,CPXF_ANCESTOR)
TNT1 A 10
Loop
LOD3:
TNT1 A 0 A_SpawnItemEx("OpenGL_AirParticle3",Random(-64.01,64.01),Random(-64.01,64.01),Random(0,8),0,0,10,0,SXF_NOCHECKPOSITION)
TNT1 A 5
Goto Spawn
LOD2:
TNT1 A 0 A_SpawnItemEx("OpenGL_AirParticle2",Random(-64.01,64.01),Random(-64.01,64.01),Random(0,8),0,0,10,0,SXF_NOCHECKPOSITION)
TNT1 A 5
TNT1 A 0 A_CheckProximity("LOD1","PlayerPawn",1280.0,1,CPXF_ANCESTOR)
Goto Spawn
LOD1:
TNT1 A 0 A_SpawnItemEx("OpenGL_AirParticle1",Random(-64.01,64.01),Random(-64.01,64.01),Random(0,8),0,0,10,0,SXF_NOCHECKPOSITION)
TNT1 A 0 A_SpawnParticle("FFFFFF",SPF_NOTIMEFREEZE|SPF_FULLBRIGHT,35,Random(2,4),0,Random(-64.01,64.01),Random(-64.01,64.01),Random(0,8),0,0,8)
TNT1 A 0 A_CheckProximity("SpawnParticles","PlayerPawn",420.0,1,CPXF_ANCESTOR)
TNT1 A 5
Goto Spawn
SpawnParticles:
TNT1 AAAAA 0 A_SpawnParticle("FFFFFF",SPF_NOTIMEFREEZE,35,Random(2,4),0,Random(-64.01,64.01),Random(-64.01,64.01),Random(0,8),0,0,8)
TNT1 A 1 A_GiveInventory("AirSpawner_Weakness",1)
Goto Wait1
Wait1:
TNT1 A 0 A_JumpIfInventory("AirSpawner_Weakness",8,"Empty")
Goto Spawn
Empty:
TNT1 A 0 A_TakeInventory("AirSpawner_Weakness",8)
TNT1 A 0 A_SpawnItemEx("MaryUmbrella_AirCurrent1",0,0,0.1,0,0,14)
Goto Spawn
}
}

actor MaryUmbrella_AirCurrent1
{
Radius 4
Height 8
Mass 1
+NOGRAVITY
States
{
Spawn:
TNT1 A 0
See:
//TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_RadiusGive("MaryUmbrella_Air",72.01,RGF_PLAYERS,1)
TNT1 AAAAAAAAAAAAA 1 A_RadiusGive("MaryUmbrella_Air",72.01,RGF_PLAYERS,1)
Goto Death
Death:
TNT1 A 1 A_FadeOut(1,1)
Loop
}
}

actor AirCurrent2 271
{
Radius 64
Height 560
+NOGRAVITY
-SOLID
//$Title Air Current (~560 high)
//$Sprite EDITAIC1
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_CheckProximity("LOD","PlayerPawn",2048.0,1,CPXF_ANCESTOR)
TNT1 A 10
Loop
LOD:
TNT1 A 0 A_SpawnParticle("FFFFFF",SPF_NOTIMEFREEZE|SPF_FULLBRIGHT,70,Random(2,4),0,Random(-128.01,128.01),Random(-128.01,128.01),Random(0,8),0,0,8)
TNT1 A 0 A_CheckProximity("SpawnParticles","PlayerPawn",384.0,1,CPXF_ANCESTOR)
TNT1 A 5
Loop
SpawnParticles:
TNT1 AAAAA 0 A_SpawnParticle("FFFFFF",SPF_NOTIMEFREEZE,70,Random(2,4),0,Random(-128.01,128.01),Random(-128.01,128.01),Random(0,8),0,0,8)
TNT1 A 1 A_GiveInventory("AirSpawner_Weakness",1)
Goto Wait1
Wait1:
TNT1 A 0 A_JumpIfInventory("AirSpawner_Weakness",8,"Empty")
Goto Spawn
Empty:
TNT1 A 0 A_TakeInventory("AirSpawner_Weakness",8)
TNT1 A 0 A_SpawnItemEx("MaryUmbrella_AirCurrent2",0,0,0.1,0,0,14)
Goto Spawn
}
}

actor MaryUmbrella_AirCurrent2 : MaryUmbrella_AirCurrent1
{
States
{
Spawn:
TNT1 A 0
See:
TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_RadiusGive("MaryUmbrella_Air",128.0,RGF_PLAYERS,1)
Goto Death
Death:
TNT1 A 1 A_FadeOut(1,1)
Loop
}
}

actor AirCurrent3 270
{
Radius 64
Height 1120
+NOGRAVITY
-SOLID
//$Title Air Current (~1,120 high)
//$Sprite EDITAIC1
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_CheckProximity("LOD","PlayerPawn",2048.0,1,CPXF_ANCESTOR)
TNT1 A 10
Loop
LOD:
TNT1 A 0 A_SpawnParticle("FFFFFF",SPF_NOTIMEFREEZE|SPF_FULLBRIGHT,140,Random(2,4),0,Random(-128.01,128.01),Random(-128.01,128.01),Random(0,8),0,0,8)
TNT1 A 0 A_CheckProximity("SpawnParticles","PlayerPawn",384.0,1,CPXF_ANCESTOR)
TNT1 A 5
Loop
SpawnParticles:
TNT1 AAAAA 0 A_SpawnParticle("FFFFFF",SPF_NOTIMEFREEZE,140,Random(2,4),0,Random(-128.01,128.01),Random(-128.01,128.01),Random(0,8),0,0,8)
TNT1 A 1 A_GiveInventory("AirSpawner_Weakness",1)
Goto Wait1
Wait1:
TNT1 A 0 A_JumpIfInventory("AirSpawner_Weakness",8,"Empty")
Goto Spawn
Empty:
TNT1 A 0 A_TakeInventory("AirSpawner_Weakness",8)
TNT1 A 0 A_SpawnItemEx("MaryUmbrella_AirCurrent3",0,0,0.1,0,0,7)
Goto Spawn
}
}

actor MaryUmbrella_AirCurrent3 : MaryUmbrella_AirCurrent1
{
States
{
Spawn:
TNT1 A 0
See:
TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_RadiusGive("MaryUmbrella_Air",128.0,RGF_PLAYERS,1)
Goto Death
Death:
TNT1 A 1 A_FadeOut(0.5,1)
Loop
}
}

actor TreeA 455
{
Radius 8
Height 64
Scale 2
Gravity 2
-SOLID
-NOGRAVITY
//$Title Tree (Forest)
//$Category Trees
//$Sprite TREEB0
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_ChangeVelocity(0,0,-6,CVF_REPLACE)  
TNT1 A 1
TNT1 A 0 A_ChangeFlag("SOLID",FALSE)
TNT1 A 0 A_FadeOut(0.999,0)
TNT1 A 0 A_JumpIf(x==0,"Check")
Goto Loop1
Check:
TNT1 A 0 A_JumpIf(y==0,"FadeOut")
Loop1:
TNT1 "#" 0 A_Jump(128, "B", "C", "A")
Loop
A:
TREE A 0
Goto WaitX
B:
TREE B 0
Goto WaitX
C:
TREE C 0
TREE C 1 A_SetScale(1.45)
WaitX:
TREE "#" -1
Stop
}
}

Actor TreeB : TreeA 1009
{
//$Title Tree (C only)
//$Sprite TREEC0
States
{
Spawn:
Goto C
}
}

actor BowlingPin 456
{
Radius 6
Height 31
Health 32767
Mass 7
Scale 1
PushFactor 1.34
PainChance 256
-SOLID
+PUSHABLE
+SHOOTABLE
+BUDDHA
+ISMONSTER	//Used for DORMANT flag to improve performance.
-COUNTKILL
+ACTIVATEMCROSS
//$Title Bowling Pin
//$Category decoration/Hell City
States
{
 Spawn:
   BPIN A 0 
   BPIN A 2	A_JumpIfInventory("CDeath",1,"Fly")
   Loop
 Pain.Stun:
   BPIN A 0 A_RadiusGive("CDeath",128.01,RGF_MONSTERS,1)	//This re-use of the cop death item may make cops angry when hitting pins.
 Pain:
 Fly:
   BPIN A 6 A_ChangeVelocity(14.5,0,7.5,CVF_RELATIVE|CVF_REPLACE)
   BPIN A 0 A_TakeInventory("CDeath",1)
   Goto Spawn
}
}

Actor CityRedLight 458
{
Radius 8
Height 16
Scale 0.75
RenderStyle Add
Alpha 0
Translation "240:254=96:111"
+NOGRAVITY
+NOINTERACTION
+NOCLIP
+BRIGHT
+FORCEXYBILLBOARD
//$Title Flashing light (red)
//$Category decoration/Hell City
//Sprite PARTA0
//$Arg0 Sprite distance
//$Arg0Default 2048
//$Arg1 Always render
//$Arg1Enum { 0 = "Falses"; 1 = "True"; }
States
{
Spawn:
PART A 0
See:
TNT1 A 0
TNT1 A 45
TNT1 A 0 A_JumpIf(Args[1]==TRUE,"Render")
TNT1 A 0 A_CheckProximity("Render","PlayerPawn",Args[0],1,CPXF_ANCESTOR)
TNT1 A 40
Goto See
Render:
TNT1 A 0 A_FadeIn(1.0)
PART AAAAAAAAAAAAAAAAAAAA 2 BRIGHT A_FadeOut(0.05,0)
Goto See
}
}

Actor CityCeilingLight1 459
{
Radius 16
Height 3
Scale 0.5
+SPAWNCEILING
+NOGRAVITY
-SOLID
+BRIGHT
//$Title Ceiling Light (Round)
//$Category decoration/Hell City
States
{
Spawn:
LITE A -1
Stop
}
}

actor WallDecorLBrown 460
{
Radius 32
Height 128
Scale 2
-SOLID
+NOGRAVITY
+NOINTERACTION
//$Title Light Brown Wall Maze
//$Category decoration/Wall Decoration
States
{
Spawn:
WALL A -1
Stop
}
}

actor FloatingGround	//Unused from NiGHTS into Dreams
{
+RANDOMIZE
+NOGRAVITY
-SOLID
States
{
Spawn:
SISL A 4
DetectDistance:
SISL A 0 A_CheckProximity("SolidX","PlayerPawn",96.0,1,CPXF_ANCESTOR)
SISL A 7 A_ChangeFlag("SOLID",FALSE)
Loop
SolidX:
SISL A 3 A_ChangeFlag("SOLID",TRUE)
Goto DetectDistance
}
}

actor CityTrash 461
{
Radius 8
Height 16
Scale 0.75
-SOLID
+NOINTERACTION
//$Title City Litter (Juice Box)
//$Category decoration/Hell City
States
{
Spawn:
JUNK A -1
Stop
}
}

actor BowlingMachine 462
{
Radius 8
Height 32
Scale 2
-SOLID
+INVULNERABLE
+NOBLOOD
+NOBLOODDECALS
//$Title Bowling Alley Machine
//$Category decoration/Hell City
States
{
 Spawn:
   BPIN C -1
   Stop
}
}

actor BowlingUpper 463
{
Radius 2
Height 16
Scale 2
-SOLID
+NOGRAVITY
+INVULNERABLE
-SHOOTABLE
+NOBLOOD
+NOBLOODDECALS
//$Title Bowling Alley Lane "Header"
//$Category decoration/Hell City
//$Arg0Name Spacer
//$Sprite BPING
States
{
 Spawn:
   BPIN E 0
   TNT1 A 0 A_JumpIf(Args[0]==1,"Spacer")
   TNT1 A 0 A_JumpIf(Args[0]==2,"Mirrored")
   BPIN E -1
   Stop
 Spacer:
   BPIN G -1
   Stop
 Mirrored:
   BPIN S -1
   Stop
}
}

actor BowlingNumber 464
{
Radius 2
Height 16
Scale 1
-SOLID
+NOGRAVITY
+INVULNERABLE
-SHOOTABLE
+NOBLOOD
+NOBLOODDECALS
//$Title Bowling Alley Lane Numbers
//$Category decoration/Hell City
//$Arg0Name Number/"Lane"
States
{
 Spawn:
   BPIN I 0
   TNT1 A 0 A_JumpIf(Args[0]==1,"No1")
   TNT1 A 0 A_JumpIf(Args[0]==2,"No2")
   TNT1 A 0 A_JumpIf(Args[0]==3,"No3")
   TNT1 A 0 A_JumpIf(Args[0]==4,"No4")
   TNT1 A 0 A_JumpIf(Args[0]==5,"No5")
   TNT1 A 0 A_JumpIf(Args[0]==6,"No6")
   TNT1 A 0 A_JumpIf(Args[0]==7,"No7")
   TNT1 A 0 A_JumpIf(Args[0]==8,"No8")
   TNT1 A 0 A_JumpIf(Args[0]==9,"No9")
   TNT1 A 0 A_JumpIf(Args[0]==10,"Lane")
   BPIN I -1
   Stop
 No1:
   BPIN J -1
   Stop
 No2:
   BPIN K -1
   Stop
 No3:
   BPIN L -1
   Stop
 No4:
   BPIN M -1
   Stop
 No5:
   BPIN N -1
   Stop
 No6:
   BPIN O -1
   Stop
 No7:
   BPIN P -1
   Stop
 No8:
   BPIN Q -1
   Stop
 No9:
   BPIN R -1
   Stop
 Lane:
   BPIN U -1
   Stop
}
}

actor CityBillboardLight 465
{
Radius 1
Height 2
Scale 2
+BRIGHT
+NOGRAVITY
-SOLID
+NOINTERACTION
//$Title Billboard Light
//$Category decoration/Hell City
States
{
Spawn:
LITE B -1
Stop
}
}

actor SoapBar 466
{
Radius 5
Height 3
Mass 15
Health 5
PainChance 0
Scale 0.5
BloodColor "White"
+NOBLOOD
-SOLID
+SHOOTABLE
//$Title Soap Bar
//$Category decoration/Hell City
States
{
Spawn:
SOAP E -1
Stop
Death:
TNT1 AAAAAAAA 0 A_SpawnItemEx("SoapPart",Random(-4,4),Random(-4,4),Random(1,2),Random(-2,2),Random(-2,2),Random(4.5,5.75),Random(0,255),SXF_NOCHECKPOSITION)
FadeOut:
TNT1 A 1 A_FadeOut(1,1)
Loop
}
}

actor SoapBox 467
{
Radius 16
Height 32
Mass 25
Health 50
-SOLID
+SHOOTABLE
+PUSHABLE
BloodColor "White"
PushFactor 1.05
DamageFactor "Bounce", 0.0
DamageFactor "Fire", 1.75
//$Title Soap Box (explodes)
States
{
Spawn:
SOAP A 4
SOAP A 0 A_SpawnItemEx("SoapBoxGlass",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_SETMASTER)
See:
SOAP A -1
Stop
Death:
TNT1 A 0 A_ChangeFlag("SOLID",TRUE)
SOAP A 1 A_ChangeVelocity(-7.5,12.75,0,CVF_REPLACE|CVF_RELATIVE)
SOAP AAAAAAAAAAAAAAAA 1 A_ChangeVelocity(-velx,-vely,0,CVF_REPLACE)
SOAP A 0 A_ChangeVelocity(0,0,0,CVF_REPLACE)
Explode:
TNT1 A 0 A_ChangeFlag("SOLID",FALSE)
TNT1 A 0 A_ScreamAndUnblock
//Spawn the upper frame parts and deadly soap...
TNT1 A 0 A_SetAngle(45+angle)
TNT1 A 0 A_SpawnItemEx("SoapBoxFrame",-16,0,32,-6.5,0,5.5,0,SXF_NOCHECKPOSITION)
TNT1 A 0 A_SpawnItemEx("SoapBoxDamagers",-5,0,10,-1.5,0,12.75,0,SXF_NOCHECKPOSITION)
TNT1 A 0 A_SetAngle(90+angle)
TNT1 A 0 A_SpawnItemEx("SoapBoxFrame",-16,0,32,-6.5,0,5.5,0,SXF_NOCHECKPOSITION)
TNT1 A 0 A_SpawnItemEx("SoapBoxDamagers",-5,0,10,-1.5,0,12.75,0,SXF_NOCHECKPOSITION)
TNT1 A 0 A_SetAngle(90+angle)
TNT1 A 0 A_SpawnItemEx("SoapBoxFrame",-16,0,32,-6.5,0,5.5,0,SXF_NOCHECKPOSITION)
TNT1 A 0 A_SpawnItemEx("SoapBoxDamagers",-5,0,10,-1.5,0,12.75,0,SXF_NOCHECKPOSITION)
TNT1 A 0 A_SetAngle(90+angle)
TNT1 A 0 A_SpawnItemEx("SoapBoxFrame",-16,0,32,-6.5,0,5.5,0,SXF_NOCHECKPOSITION)
TNT1 A 0 A_SpawnItemEx("SoapBoxDamagers",-5,0,10,-1.5,0,12.75,0,SXF_NOCHECKPOSITION)
TNT1 A 0 A_SetAngle(45+angle)
TNT1 A 0 A_SpawnItemEx("SoapBoxDamagers",0,0,12.5,0,0,14.25,0,SXF_NOCHECKPOSITION)
//...then the harmless soap...
TNT1 AAAAAAAAAAAAAAAA 0 A_SpawnItemEx("SoapPart",Random(-6,6),Random(-6,6),Random(3,9),Random(-3,3),Random(-3,3),Random(5.5,7),Random(0,255),SXF_NOCHECKPOSITION)
//...then lastly, spawn the bottom frame leftovers and explode!
SOAP D 0 A_SpawnItemEx("SoapBoxFrameBottom",0,0,0,0,0,6.5,0,SXF_NOCHECKPOSITION)
FadeOut:
TNT1 A 1 A_FadeOut(1,1)
Loop
Burn:
TNT1 A 0 A_Quake(3,17,0,192)
TNT1 A 0 A_Quake(2,35,0,208)
TNT1 A 0 A_Quake(1,50,0,224)
Goto Explode
Ice:
SOAP A 0 A_ChangeFlag("SOLID",TRUE)
SOAP A 0 A_ChangeFlag("PUSHABLE",TRUE)
SOAP A 140 A_SpawnItemEx("IceBlock_SoapBox",0,0,0,0,0,0,0,SXF_SETMASTER|SXF_NOCHECKPOSITION)
Goto Death
}
}
actor SoapBoxGlass
{
Radius 16
Height 32
Health 1000
RenderStyle Add
Alpha 0.13
+DONTRIP
+DONTTHRUST
+NOBLOOD
-SHOOTABLE
-SOLID
+NODAMAGETHRUST
States
{
Spawn:
SOAP B 1 NoDelay
SOAP B 0 A_Warp(AAPTR_MASTER,0,0,0,0,0,"Spawn")
Goto FadeOut
Death:
TNT1 A 1 A_FadeOut(1,1)
Loop
}
}
actor IceBlock_SoapBox	//This won't work for anything but the soap box due to its specific behavior.
{
Radius 20
Height 36
Health 1000
RenderStyle Add
Alpha 0.5
Scale 1.13
+DONTRIP
+DONTTHRUST
+NOBLOOD
-SHOOTABLE
-SOLID
+NODAMAGETHRUST
States
{
Spawn:
ICEB AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 NoDelay A_Warp(AAPTR_MASTER)
ICEB A 17
Goto FadeOut
Death:
TNT1 A 1 A_FadeOut(1,1)
Loop
}
}

actor SoapBoxFrame
{
Radius 12
Height 24
Scale 1.5
-SOLID
States
{
Spawn:
SOAP C -1
Stop
}
}

actor SoapBoxFrameBottom	//Leftovers after soap box explodes, used to make its glass disappear.
{
Radius 16
Height 16
-SOLID
States
{
Spawn:
SOAP D 0
SOAP D 1 A_Explode((40),84,0,FALSE,48)
SOAP D -1
Stop
}
}

Actor SoapBoxDamagers	//Somewhat unobvious name, but this is the liquid soap that flies out of the Soap Box when destroyed.
{
Radius 4
Height 8
Scale 0.13
DamageType "Soap"
PROJECTILE
-NOGRAVITY
States
{
Spawn:
SOAP G 0 NoDelay A_Jump(128,"Pain")
See:
SOAP G -1
Stop
Pain:
SOAP G 0 A_SetScale(-scalex)
Goto See
Death:
SOAP H 0 A_ChangeFlag("FLATSPRITE",TRUE)
SOAP H 0 A_SetScale(scalex+0.63)
SOAP HHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHH 2 { A_Explode((1),32,0,FALSE,16); A_SetScale(scalex-0.005); A_FadeOut(0.006); A_SetAngle(angle+0.66, SPF_INTERPOLATE); }
FadeOut:
SOAP H 1 { A_SetScale(scalex-0.005); A_FadeOut(0.006); }
Loop
}
}

Actor DrillEnd 468
{
Radius 12
Height 24
Scale 2
-SOLID
+NOGRAVITY
+NODAMAGETHRUST
+NOBLOOD
//$Title Drill End
//$Category decoration/Hell City
States
{
Spawn:
JUNK C 8 { If(A_CheckProximity("Spawn","PlayerPawn",512.0,1,CPXF_ANCESTOR)) { A_ChangeFlag("SOLID",TRUE); } Else { A_ChangeFlag("SOLID",FALSE); } }
Stop
}
}

Actor UroborosLaserSpawner 469
{
Radius 8
Height 4
+NOGRAVITY
+ISMONSTER
+SHOOTABLE
-COUNTKILL
+DONTTHRUST
+NOBLOOD
+INVULNERABLE
+GHOST
//$Title Uroboros laser beam spawner
//$Category decoration/Hell City
States
{
Spawn:
TNT1 A 1 NoDelay A_Look
Loop
See:
TNT1 A 0 A_FaceTarget
TNT1 A 1 A_CustomMissile("UroborosLBeam",2,0,0,0)
Loop
}
}

Actor XaneCorpLight2 : UroborosLaserSpawner
{
Radius 1
Height 2
RenderStyle Add
Alpha 0.5
Scale 0.5
+NOGRAVITY
+ISMONSTER
+SHOOTABLE
-COUNTKILL
//$Title Uroboros laser beam spawner
//$Category decoration/Hell City
States
{
Spawn:
PART A 1 NoDelay A_Look
Loop
See:
PART A 0 A_FaceTarget
PART A 1 A_CustomMissile("UroborosLBeam",2,0,0,0)
Loop
}
}

Actor Rain1000
{
Scale 1
VSpeed 0
Alpha 0
RenderStyle Normal
Args 3, 4, 0
//PROJECTILE
+NOGRAVITY
-SOLID
States
{
Spawn:
TNT1 A 0
TNT1 A 0 ACS_NamedExecuteAlways("Snow_CheckSky",0,0,0,0)
TNT1 A 1
Choose:
TNT1 A 0 A_Jump(128,"B")
Goto "A"
A:
SPRK A 0
Goto Move
B:
SPRK B 0
Goto Move
Move:
SPRK "#" 0 A_ChangeVelocity(Random(-2.5,2.5),Random(-2.5,2.5),-3,CVF_REPLACE)
Goto FadeIn
FadeIn:
TNT1 AA 1 A_FadeIn(0.5)
See:
TNT1 A 0 A_CheckProximity("IndoorCheck","LGirlPlayer",512,1)
TNT1 A 0 A_CheckProximity("IndoorCheck","XanePlayer",512,1)
Fire:
TNT1 AAAAA 1 A_SpawnParticle("FFFFFF",false,28,Random(3,5),0,Random(-96.5,96.5),Random(-96.5,96.5),0,CallACS("GetXProperty",args[0],0,0,0),CallACS("GetXProperty",args[1],0,0,0),-11.25,0,0,0,1,0)
Death:
FadeOut:
TNT1 A 1 A_FadeOut(1,1)
Loop
IndoorCheck:	//This checks for if the player this is associated with is indoors.
TNT1 A 0 A_JumpIf(CallACS("Snow_CheckSky",TRUE,Args[2],0,0)==TRUE,"SingleRaindrop")
Goto Fire
SingleRaindrop:
TNT1 A 0 A_ChangeFlag("SOLID", TRUE)
TNT1 A 3 A_ChangeVelocity(Random(-3,3),Random(-3,3),0.0,CVF_REPLACE)
TNT1 A 0 A_SpawnParticle("FFFFFF",false,28,Random(3,5),0,0,0,0,CallACS("GetXProperty",args[0],0,0,0),CallACS("GetXProperty",args[1],0,0,0),-11.25,0,0,0,1,0)
TNT1 A 5 A_ChangeVelocity(Random(-5.5,5.5),Random(-5.5,5.5),0.0,CVF_REPLACE)
TNT1 A 0 A_SpawnParticle("FFFFFF",false,28,Random(3,5),0,0,0,0,CallACS("GetXProperty",args[0],0,0,0),CallACS("GetXProperty",args[1],0,0,0),-11.25,0,0,0,1,0)
TNT1 A 7 A_ChangeVelocity(Random(-8,8),Random(-8,8),0.0,CVF_REPLACE)
TNT1 A 0 A_SpawnParticle("FFFFFF",false,28,Random(3,5),0,0,0,0,CallACS("GetXProperty",args[0],0,0,0),CallACS("GetXProperty",args[1],0,0,0),-11.25,0,0,0,1,0)
Goto FadeOut
}
}

Actor Rain1001 : Rain1000 { Args 5, 6, 1 }
Actor Rain1002 : Rain1000 { Args 7, 8, 2 }
Actor Rain1003 : Rain1000 { Args 9, 10, 3 }
Actor Rain1004 : Rain1000 { Args 11, 12, 4 }
Actor Rain1005 : Rain1000 { Args 13, 14, 5 }
Actor Rain1006 : Rain1000 { Args 15, 16, 6 }
Actor Rain1007 : Rain1000 { Args 17, 18, 7 }

Actor Snow1000
{
Scale 1
VSpeed 0
Alpha 0.01
RenderStyle Normal
Args 3, 4
//PROJECTILE
+NOGRAVITY
+FORCEXYBILLBOARD
States
{
Spawn:
TNT1 A 0 NoDelay ACS_NamedExecuteAlways("Snow_CheckSky",0,0,0,0)
TNT1 A 1
Choose:
TNT1 A 0 A_Jump(128,"B")
Goto "A"
A:
SPRK A 0
Goto Move
B:
SPRK B 0
Goto Move
Move:
SPRK "#" 0 A_ChangeVelocity(Random(-2.5,2.5)+CallACS("GetXProperty",args[0],0,0,0),Random(-2.5,2.5)+CallACS("GetXProperty",args[1],0,0,0),-3,CVF_REPLACE)
FadeIn:
SPRK "#" 1 A_FadeIn(0.1)
SPRK "#" 1 A_FadeIn(0.1)
SPRK "#" 1 A_FadeIn(0.1)
SPRK "#" 1 A_FadeIn(0.1)
SPRK "#" 1 A_FadeIn(0.1)
SPRK "#" 1 A_FadeIn(0.1)
SPRK "#" 1 A_FadeIn(0.1)
SPRK "#" 1 A_FadeIn(0.1)
SPRK "#" 1 A_FadeIn(0.1)
SPRK "#" 1 A_FadeIn(0.1)
See:
SPRK "#" 2 A_JumpIf(velz==0.0||z-floorz<4.5||abs(min(velx,vely))<0.5,"Death")
Loop
Death:
FadeOut:
TNT1 A 1 A_FadeOut(1,1)
Loop
}
}

Actor Snow1001 : Snow1000 { Args 5, 6 }
Actor Snow1002 : Snow1000 { Args 7, 8 }
Actor Snow1003 : Snow1000 { Args 9, 10 }
Actor Snow1004 : Snow1000 { Args 11, 12 }
Actor Snow1005 : Snow1000 { Args 13, 14 }
Actor Snow1006 : Snow1000 { Args 15, 16 }
Actor Snow1007 : Snow1000 { Args 17, 18 }

Actor BubbleSpawner 150
{
+RANDOMIZE
+NOGRAVITY
+NOINTERACTION
+NOBLOCKMAP
//$Title Bubble Spawner
//$Sprite BUBBA0
States
{
Spawn:
TNT1 A 4
TNT1 A 0 A_CheckProximity("See","LGirlPlayer",512,1)
TNT1 A 0 A_CheckProximity("See","XanePlayer",512,1)
TNT1 A 2
Loop
See:
TNT1 A Random(10,15)
TNT1 A 0 A_Jump(160,"A")
TNT1 A 0 A_Jump(144,"B","C")
Loop
A:
TNT1 AAAAA Random(6,9) A_SpawnItemEx("BubbleA",Random(-6.5,6.5),Random(-6.5,6.5),0,0,0,Random(1,1.5),Random(0,359),SXF_NOCHECKPOSITION,64)
TNT1 A Random(5,7)
TNT1 A 0 A_Jump(128,"B")
Goto Spawn
B:
TNT1 A Random(5,9) A_SpawnItemEx("BubbleB",Random(-6.5,6.5),Random(-6.5,6.5),0,0,0,Random(1,1.5),Random(0,359),SXF_NOCHECKPOSITION,0)
TNT1 AA Random(4,7) A_SpawnItemEx("BubbleA",Random(-6.5,6.5),Random(-6.5,6.5),0,0,0,Random(1,1.5),Random(0,359),SXF_NOCHECKPOSITION,0)
TNT1 A Random(8,13)
TNT1 A 0 A_Jump(72,"C")
Goto Spawn
C:
TNT1 A 0 A_Jump(32,"D")
TNT1 A Random(1,4) A_SpawnItemEx("BubbleA",Random(-6.5,6.5),Random(-6.5,6.5),0,0,0,Random(1,1.5),Random(0,359),SXF_NOCHECKPOSITION,0)
TNT1 A Random(1,4) A_SpawnItemEx("BubbleB",Random(-6.5,6.5),Random(-6.5,6.5),0,0,0,Random(1,1.5),Random(0,359),SXF_NOCHECKPOSITION,0)
TNT1 A Random(1,4) A_SpawnItemEx("BubbleA",Random(-6.5,6.5),Random(-6.5,6.5),0,0,0,Random(1,1.5),Random(0,359),SXF_NOCHECKPOSITION,0)
TNT1 A Random(4,7) A_SpawnItemEx("BubbleC",Random(-6.5,6.5),Random(-6.5,6.5),0,0,0,Random(1,1.5),Random(0,359),SXF_NOCHECKPOSITION,0)
TNT1 A Random(9,16)
Goto A
D:
TNT1 AA Random(1,2) A_SpawnItemEx("BubbleA",Random(-6.5,6.5),Random(-6.5,6.5),0,0,0,Random(1,1.5),Random(0,359),SXF_NOCHECKPOSITION,0)
TNT1 A Random(1,3) A_SpawnItemEx("BubbleB",Random(-6.5,6.5),Random(-6.5,6.5),0,0,0,Random(1,1.5),Random(0,359),SXF_NOCHECKPOSITION,0)
TNT1 AA Random(2,3) A_SpawnItemEx("BubbleA",Random(-6.5,6.5),Random(-6.5,6.5),0,0,0,Random(1,1.5),Random(0,359),SXF_NOCHECKPOSITION,0)
TNT1 A Random(7,12) A_SpawnItemEx("BubbleD",Random(-6.5,6.5),Random(-6.5,6.5),0,0,0,Random(1,1.5),Random(0,359),SXF_NOCHECKPOSITION,0)
TNT1 A Random(2,3) A_SpawnItemEx("BubbleB",Random(-6.5,6.5),Random(-6.5,6.5),0,0,0,Random(1,1.5),Random(0,359),SXF_NOCHECKPOSITION,0)
TNT1 AA Random(1,4) A_SpawnItemEx("BubbleA",Random(-6.5,6.5),Random(-6.5,6.5),0,0,0,Random(1,1.5),Random(0,359),SXF_NOCHECKPOSITION,0)
TNT1 A Random(8,10)
Goto B
}
}

Actor BubbleA : CustomInventory
{
Radius 2
Height 4
Scale 0.01
VSpeed 1
WeaveIndexXY 2	
WeaveIndexZ 2
RenderStyle Add
Inventory.PickupMessage " "
+NOGRAVITY
States
{
Spawn:
BUBB A 0
Goto Pain
Pain:
BUBB A 1 A_SetScale(scalex+0.05,scaley+0.05)
BUBB A 0 A_Weave(1,0.25,1.5,0.25)
BUBB A 0 A_JumpIf(scalex>0.35,"See")
Loop
See:
BUBB A 0 A_JumpIf(waterlevel<3,"Death")
BUBB A 0 A_JumpIf(z>ceilingz-32,"Death")
BUBB A 1 A_Weave(1,0.25,1.5,0.25)
Loop
Death:
TNT1 AAAAAAA 0 A_SpawnParticle("White",SPF_RELATIVE|SPF_RELANG|SPF_RELACCEL,17,3,Random(0,359),0,0,0,Random(-2.75,2.75),Random(-2.75,2.75),Random(-2.25,2.25),0,0,0,0.9,-1)
Goto FadeOut
FadeOut:
TNT1 A 1 A_FadeOut(0.5,1)
Loop
Pickup:
Goto Fail
}
}

Actor BubbleB : BubbleA
{
Radius 4
Height 8
States
{
Spawn:
BUBB A 0
Goto Pain
Pain:
BUBB A 1 A_SetScale(scalex+0.1,scaley+0.1)
BUBB A 0 A_Weave(1,0.25,1.5,0.25)
BUBB A 0 A_JumpIf(scalex>0.75,"See")
Loop
}
}

Actor BubbleC : BubbleB
{
Radius 8
Height 16
States
{
Spawn:
BUBB A 0
Goto Pain
Pain:
BUBB A 1 A_SetScale(scalex+0.2,scaley+0.2)
BUBB A 0 A_Weave(1,0.25,1.5,0.25)
BUBB A 0 A_JumpIf(scalex>1.45,"See")
Loop
Pickup:
TNT1 AAAAAAAAAA 0 A_SpawnParticle("White",SPF_RELATIVE|SPF_RELANG|SPF_RELACCEL,17,3,Random(0,359),0,0,0,Random(-2.75,2.75),Random(-2.75,2.75),Random(-2.25,2.25),0,0,0,0.9,-1)
BUBB A 0 ACS_NamedExecuteAlways("GiveAir",0,15,0,0)
Stop
Death:
TNT1 AAAAAAAAAAA 0 A_SpawnParticle("White",SPF_RELATIVE|SPF_RELANG|SPF_RELACCEL,17,Random(4,5),Random(0,359),0,0,0,Random(-2.75,2.75),Random(-2.75,2.75),Random(-2.25,2.25),0,0,0,0.9,-1)
Goto FadeOut
}
}

Actor BubbleD : BubbleC
{
Radius 12
Height 24
Inventory.PickupMessage "$TXT_BUBBLEBIG"
States
{
Spawn:
BUBB A 0
Goto Pain
Pain:
BUBB A 1 A_SetScale(scalex+0.3,scaley+0.3)
BUBB A 0 A_Weave(1,0.25,1.5,0.25)
BUBB A 0 A_JumpIf(scalex>2.5,"See")
Loop
Pickup:
TNT1 AAAAAAAAAAAAAA 0 A_SpawnParticle("White",SPF_RELATIVE|SPF_RELANG|SPF_RELACCEL,17,3,Random(0,359),0,0,0,Random(-2.75,2.75),Random(-2.75,2.75),Random(-2.25,2.25),0,0,0,0.9,-1)
BUBB A 0 ACS_NamedExecuteAlways("GiveAir",0,30,0,0)
Stop
}
}

Actor XGlass_Spawner
{
+INVISIBLE
+NOINTERACTION
+NOGRAVITY
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_Turn(-25)
TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("XGlass",0,Random(-42,42),Random(-36,36),Random(4.25,6.13),Random(-1.75,1.75),Random(3.1,5.1),SXF_NOCHECKPOSITION)
FadeOut:
TNT1 A 1 A_FadeOut(1,1)
Loop
}
}
Actor RockParticle_Spawner
{
+INVISIBLE
+NOINTERACTION
+NOGRAVITY
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_Turn(-25)
TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("RockParticle",0,Random(-42,42),Random(-36,36),Random(4.25,6.13),Random(-1.75,1.75),Random(3.1,5.1),SXF_NOCHECKPOSITION)
FadeOut:
TNT1 A 1 A_FadeOut(1,1)
Loop
}
}

Actor TallTorch 450
{
Radius 5
Height 80
Scale 2.5
+SOLID
States
{
Spawn:
LITE C 1 A_SpawnItemEx("TallTorchFire",0,0,72,0,0,0,0,SXF_NOCHECKPOSITION)
Loop
}
}
Actor TallTorchFire
{
Radius 3
Height 6
-SOLID
+NOGRAVITY
+NOINTERACTION
+INVISIBLE
+NOBLOCKMAP
States
{
Spawn:
TNT1 A 0 NoDelay A_SpawnParticle("BrightRed",SPF_FULLBRIGHT|SPF_INTERPOLATE|SPF_RELVEL,10,15,0,Random(-7.5,7.5),Random(-7.5,7.5),Random(3.5,11.5), 0, 0, 2.9)
TNT1 A 0 NoDelay A_SpawnParticle("LightOrange",SPF_FULLBRIGHT|SPF_INTERPOLATE|SPF_RELVEL,8,12,0,Random(-6,6),Random(-6,6),Random(3.5,10.5), 0, 0, 3.75)
TNT1 A 0 NoDelay A_SpawnParticle("BrightYellow",SPF_FULLBRIGHT|SPF_INTERPOLATE|SPF_RELVEL,6,9,0,Random(-4.5,4.5),Random(-4.5,4.5),Random(4.5,12.25), 0, 0, 4.25)
TNT1 A 0 NoDelay A_SpawnParticle("Grey",SPF_FULLBRIGHT|SPF_INTERPOLATE|SPF_RELVEL,35,25,0,Random(-9.5,9.5),Random(-9.5,9.5),Random(3.75,9.5), 0, 0, 2.25,0,0,0,0.75)
TNT1 A 1
Stop
}
}

Actor ToiletPaper 451
{
Radius 10
Height 14
+NOGRAVITY
+SOLID
+NODAMAGETHRUST
+INVULNERABLE
//$Title Toilet Paper Roll
//$Category decoration/Hell City
States
{
Spawn:
JUNK D -1
Stop
}
}


Actor AlarmSound 500
{
+NOBLOCKMAP
+NOINTERACTION
+RANDOMIZE
+NOGRAVITY
+SPAWNCEILING
States
{
Spawn:
TNT1 A 10
TNT1 A 3
See:
TNT1 A 17 A_StopSound(CHAN_ITEM)
TNT1 A 0 A_CheckProximity("CheckWL","PlayerPawn",768,1,CPXF_ANCESTOR)
Loop
CheckWL:
TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("GetWLevel",0,0,0,0)>2,"Alarm")
Goto See
Alarm:
TNT1 A 0 A_PlaySound("misc/alarm",CHAN_ITEM,1.0,TRUE,0.5)
Pain:
TNT1 A 17
TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("GetWLevel",0,0,0,0)<3,"See")
TNT1 A 0 A_CheckProximity("Pain","PlayerPawn",768,1,CPXF_ANCESTOR)
Goto See
}
}