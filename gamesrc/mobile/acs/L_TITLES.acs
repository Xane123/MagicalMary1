#library "xglobal"
#include "zcommon.acs"

world int 4:world_info[];	//This holds the world number ([0]) and level number within that world ([1]) to form things like "1-1".

global bool 6:fade;	//Requested fade upon level transition.
global int 20:level[];	//Level number; Don't set this if challenge[0] isn't 0. [0] is highest level reached and [1] is last level played. Use each in different situations.
global int 25:gamemode;	//0: Normal, 1: Time attack, 2: Boss rush

#define CONST_MAX_LEVELS 200

script "TitleCard" (void)
{
	If(GetLevelInfo(LEVELINFO_LEVELNUM)>=CONST_MAX_LEVELS-2)
	{
		if(fade==0) fade = 1;	//Loading screens would look ugly without a solid background color.		
		if(fade==1) FadeTo(0,0,0,1.0,0.0);
		if(fade==2) FadeTo(255,255,255,1.0,0.0);
		Terminate;
	}
	
	Delay(8);
	
	If(ACS_NamedExecuteWithResult("CheckSpecialStage",0,0,0,0)==TRUE||(GameSkill()==3&&GetLevelInfo(LEVELINFO_LEVELNUM)==33)) Terminate;
	If(world_info[0]==0) { ACS_NamedExecuteAlways("SmallTitle",0,0,0,0); Terminate; }	//This is the hut, so display a unique title card, a'la the Hall of Challenges previously.
	
	for (int md = -0.75; md < 0; md += 0.05)
	{
		if(md<=-0.05&&md>-0.25)
		{
			SetFont("LTITLEBX");
			HUDMessage (s:"A";
				HUDMSG_FADEOUT, 0, CR_CREAM, 0.225+md,0.001, 0.001, 0.001, 0.001);   //Rectangle
		}
		SetFont("TITLEFNT");
		If(gamemode!=2)  HUDMessage (d:world_info[0], s:"-", d:world_info[1];
			HUDMSG_FADEOUT, 0, CR_CREAM, 0.25-md,0.65, 0.001, 0.001, 0.001);   //Text (eg, "1-1")
		Else 
		{
			if(md<=-0.05&&md>-0.5) HUDMessage (s:"BOSS ", d:world_info[0];
				HUDMSG_FADEOUT, 0, CR_CREAM, 0.39-md,0.65, 0.001, 0.001, 0.001);   //Text (eg, "BOSS 1")
		}
		
		if(md<=0&&md>-0.05)
			{
			SetFont("LTITLEBX");
			HUDMessage (s:"A";
				HUDMSG_FADEOUT, 0, CR_CREAM, 0.225+md,0.001, 3.5, 1.0, 1.0);   //Rectangle
			SetFont("TITLEFNT");
			If(gamemode!=2) HUDMessage (d:world_info[0], s:"-", d:world_info[1];
				HUDMSG_FADEOUT, 0, CR_CREAM, 0.257-md,0.65, 3.5, 1.0, 1.0);   //Text (eg, "1-1")
			Else { HUDMessage (s:"BOSS ", d:world_info[0];
				HUDMSG_FADEOUT, 0, CR_CREAM, 0.39-md,0.65, 3.5, 1.0, 1.0); }   //Text (eg, "BOSS 1")
			SetFont("BIG_TEXT");
			
			HUDMessage (l:StrParam(s:"LEVELNAME_", d:GetLevelInfo(LEVELINFO_LEVELNUM)+1);
				HUDMSG_FADEINOUT, 0, CR_BRICK, 0.175-md+(StrLen(StrParam(l:StrParam(s:"LEVELNAME_", d:GetLevelInfo(LEVELINFO_LEVELNUM)+1)))*0.016),0.75, 3.0, 0.5, 1.0, 1.0);   //Level name (LANGUAGE.enu)
			
			}
		Delay(1);
	}
}

script "SmallTitle" (int number)
{
	int lvl_temp = level[1]+1;
	if(lvl_temp>200) lvl_temp -= 200;
	
	If(number<0) Terminate;
	If(number==0)
	{
		HUDMessage (l:StrParam(s:"LEVELNAME_",d:lvl_temp);
			HUDMSG_TYPEON, 0, CR_BRICK, 0.5,0.75, 2.0, 0.05, 1.0);   //Level name (LANGUAGE.enu)
	}
	Else
	{
		HUDMessage (l:StrParam(s:"STITLE_",d:number);
			HUDMSG_TYPEON, 0, CR_BRICK, 0.5,0.75, 2.0, 0.05, 1.0);   //Building/area name (LANGUAGE.enu)
	}
}